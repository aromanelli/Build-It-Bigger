apply plugin: 'com.android.application'
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
// Moved to project build.gradle
//        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:2.0.0'
    }
}

android {

    // Configure only for each module that uses Java 8
    // language features (either in its source code or
    // through dependencies).
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion 28

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // https://developer.android.com/studio/build/build-variants
    // Specifies one flavor dimension.
    flavorDimensions "version"
    productFlavors {
        free {
            // Assigns this product flavor to the "version" flavor dimension.
            // This property is optional if you are using only one dimension.
            dimension "version"
            applicationIdSuffix ".free"
            versionNameSuffix "-free"
        }
        paid {
            dimension "version"
            applicationIdSuffix ".paid"
            versionNameSuffix "-paid"
        }
    }

    /* TODO REVISIT THIS; FIND BEST SOLUTION. Be sure to read last answer in below SO link!
    https://stackoverflow.com/questions/44342455/more-than-one-file-was-found-with-os-independent-path-meta-inf-license

    Exception: Caused by: com.android.builder.merge.DuplicateRelativeFileException:
    More than one file was found with OS independent path 'META-INF/DEPENDENCIES'
	at com.android.builder.merge.StreamMergeAlgorithms.lambda$acceptOnlyOne$2(StreamMergeAlgorithms.java:75)

    Basically when gradle puts together the apk file, it copies content from all the compile
    dependencies, It is intelligent enough to see that there is a duplicate file..coming from
    two different jar files. This could be any file like a.txt or META-INF/DEPENDENCIES. It
    might be best to exclude these files using the below, in case the file is present there
    only for informational purposes.
    */
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
    /*
    The solutions here didn't help me, but this link did:
    https://groups.google.com/forum/#!topic/Android-ndk/vfMfld5FY7A

    If you have a library that's adding some android .so files –like libassmidi.so or
    libgnustl_shared.so– you have to tell gradle to pick just one when packaging,
    otherwise you'll get the conflict.

    android {
      packagingOptions {
        pickFirst 'httpclient-4.5.3.jar' // httpcore-4.4.6.jar
        pickFirst 'lib/armeabi-v7a/libassmidi.so'
        pickFirst 'lib/x86/libassmidi.so'
      }
    }
    */

    dataBinding.enabled = true
}

dependencies {

    implementation project(':jokes')
    implementation project(':jokeviewer')

    implementation 'com.android.support:multidex:1.0.3'

    implementation 'com.google.guava:guava:26.0-android'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    endpointsServer project(path: ':backend', configuration: 'endpoints')

    implementation 'com.android.support:support-v4:28.0.0-rc01'
    implementation 'com.android.support:customtabs:28.0.0-rc01'
    implementation 'com.android.support:appcompat-v7:28.0.0-rc01'
    implementation 'com.android.support:design:28.0.0-rc01'

    freeImplementation 'com.google.android.gms:play-services-ads:15.0.1'

    implementation 'com.google.api-client:google-api-client:1.25.0'
    implementation 'com.google.http-client:google-http-client-android:1.25.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.2' // Also needed by ProGuardWithGuava proguard-rules.pro

    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'

    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testImplementation 'junit:junit:4.12'
}
